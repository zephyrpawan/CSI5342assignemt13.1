model RBAC

-- classes
-- ***************************************************************
class Role
attributes
  name : String
operations
  grantPermission(p : Permission)
  revokePermission(p : Permission)

  checkAccess(obj : Object, op : Operation) : Boolean =
    self.permission->exists(p | p.operation->includes(op)) and
    self.permission->exists(p | p.object->includes(obj)) and
    op.object->includes(obj)

  addAscendant(r : Role)
  deleteAscendant(r : Role)

  addDescendant(r : Role)
  deleteDescendant(r : Role)

  addSSDRole(r : Role)
  deleteSSDRole(r : Role)

  addDSDRole(r : Role)
  deleteDSDRole(r : Role)

  assignedUsers() : Set(User)
  authorizedUsers() : Set(User)

  ascendants() : Set(Role)
  descendants() : Set(Role)

  ssdRoles() : Set(Role)
  dsdRoles() : Set(Role)
end

-- ***************************************************************
class User
attributes
  userID : String
  name: String
operations
  createSession(s : Session)
  deleteSession(s : Session)
  assignRole(r : Role)
  deassignRole(r : Role)
  assignedRoles() : Set(Role)
  authorizedRoles() : Set(Role)
end

-- ***************************************************************
class Permission
operations
  checkAccess(obj : Object, op : Operation) : Boolean =
    self.object->includes(obj) and
    self.operation->includes(op) and
    op.object->includes(obj)
end

-- ***************************************************************
class Object
attributes
  name : String
end

-- ***************************************************************
class Operation
attributes
  name : String
end

-- ***************************************************************
class Session
attributes
  name : String
operations
  addActiveRole(r : Role)
  dropActiveRole(r : Role)
  checkAccess(obj : Object, op : Operation) : Boolean =
    self.role.permission->exists(p | p.operation->includes(op)) and
    self.role.permission->exists(p | p.object->includes(obj)) and
    op.object->includes(obj)
  invokeOperation(obj : Object, op : Operation)
end
-- ***************************************************************

-- associations
association UserAssignment between
  User[1..*] role user
  Role[1..*] role role
end

-- ***************************************************************
association PermAssignment between
  Permission[1..*] role permission
  Role[1..*] role role
end

-- ***************************************************************
association UserSessions between
  User[1] role user
  Session[1..*] role session
end

-- ***************************************************************
association SessionRoles between
  Session[*] role session
  Role[*] role role
end

-- ***************************************************************
association PermObjects between
  Permission[1..*] role permission
  Object[1..*] role object
end

-- ***************************************************************
association PermOperations between
  Permission[1..*] role permission
  Operation[1..*] role operation
end

-- ***************************************************************
association ExecuteOn between
  Operation[*] role operation
  Object[*] role object
end

-- ***************************************************************
association RoleHierarchy between
  Role[*] role senior
  Role[*] role junior
end

-- ***************************************************************
association SSD between
  Role[*] role rs1
  Role[*] role rs2
end

-- ***************************************************************
association DSD between
  Role[*] role rd1
  Role[*] role rd2
end

-- ***************************************************************
-- OCL constraints
constraints

context User :: createSession(s : Session)
  post: s.oclIsNew() = true and self.session->includes(s)

-- Permission can be granted if missing
context Role :: grantPermission(p : Permission)
  pre: self.permission->excludes(p)
  post: self.permission->includes(p)

-- Permission can be removed if present
context Role :: revokePermission(p : Permission)
  pre: self.permission->includes(p)
  post: self.permission->excludes(p)

-- SSD-Role constraint: Conflicting roles cannot be assigned to the same user
context Role
  inv SSDRole:
    rs1.user->excludesAll(rs2.user)

-- SSD-Permission constraint: Conflicting permissions cannot be assigned to the same role.
context Permission
  inv SSDPermission:
    Permission.allInstances->forAll(p1, p2 | p1 <> p2 implies p1.role->excludesAll(p2.role))

-- SSD constraint: A user cannot be assigned to two roles that are involved in an SSD relation
context User
  inv SSDUserConstraint:
    self.role->forAll(r1, r2 | r1.rs1->excludes(r2))

-- DSD constraint. A user cannot activate two roles in DSD relation within a session.
context User
  inv DSDUserConstraint:
    self.session.role->forAll(r1, r2 | r1.rd1->excludes(r2))

-- Hierarchical SSD constraint.
-- There cannot be roles in an SSD relation which have the same senior role.
context Role
 inv HierarchicalSSDConstraint:
   self.senior->forAll(j1, j2 | j1.junior->excludesAll(j2.senior))

-- This precondition is optional as the Permission.checkAccess will return false in this case
--context Permission::checkAccess(obj : Object, op : Operation) : Boolean
--  pre: self.object->includes(obj) and self.operation->includes(op)

-- Each permission must be associated with 1 or more objects and operations
context Permission
  inv minimumOperationsAndObjects:
    self.operation->size() > 0 and self.object->size() > 0

-- Each object must be have at least a permission for 1 operation
context Object
  inv minPermissionOnObject:
    self.permission->size() > 0 and self.operation->size() > 0

-- Each operation must be associated with a permission to be executed on a object
context Operation
 inv minPermissionAndOperations:
   self.permission->size() > 0 and self.object->size() > 0

-- A role must have at least 1 permission and at least 1 user assigned
context Role
  inv minPermission:
    self.permission->size() > 0 and self.user->size() > 0

-- A session can have only 1 user tied to it
context Session
  inv sessionIsFor1SpecificUser:
    self.user->size() = 1

--if permission.checkAccess returns true, then role.grantPermission, else role.revokePermission

--if role.checkAccess returns true, then session.addActiveRole, else session.dropActiveRole

--if session.checkAccess returns true, then user.createSession, else user.deleteSession

